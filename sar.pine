//@version=4
study("Lucid SAR version 1.0.0", shorttitle="Lucid SAR", overlay=true)

// Branded under the name "Lucid SAR" 
// As agreed to with Lucid Investment Strategies LLC on July 9, 2019
// https://lucidinvestmentstrategies.com/


// Created by Casey Bowman on July 4, 2019

// MIT License

// Copyright (c) 2019 Casey Bowman

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.


AF_initial = input(0.02)
AF_increment = input(0.02)
AF_maximum = input(0.2)


// start with uptrend
var uptrend = true
var newtrend = false
var EP = high
var SAR = low
var AF = AF_initial

if bar_index > 0
    if uptrend
        EP := max(high, EP[1])
    else
        EP := min(low, EP[1])
    
    if EP != EP[1]
        AF := min(AF_maximum, AF + AF_increment)
    
    SAR := SAR[1] + AF * (EP - SAR[1])
    
    if uptrend
        if SAR > low
            uptrend := false
            newtrend := true
            SAR := max(high, EP[1])
            EP := min(low, low[1])
            AF := AF_initial
        else
            newtrend := false
    else
        if SAR < high
            uptrend := true
            newtrend := true
            SAR := min(low, EP[1])
            EP := max(high, high[1])
            AF := AF_initial
        else
            newtrend := false
            
    if uptrend
        SAR := min(SAR, low[1])
        if not na(low[2])
            SAR := min(SAR, low[2])
    else
        SAR := max(SAR, high[1])
        if not na(high[2])
            SAR := max(SAR, high[2])

// Debug values
// plot(EP, color = uptrend ? color.lime : color.orange, style = plot.style_circles, linewidth = 2)
// plot(EP != EP[1] ? EP : na, color = uptrend ? color.green : color.red, style = plot.style_circles, linewidth = 5)
// plot(AF, color= color.purple, style=plot.style_circles)
plot(SAR, color = color.blue, style = plot.style_cross, linewidth = 2)
